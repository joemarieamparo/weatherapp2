apply plugin: 'jacoco'

ext {
    coverageSourceDirs = 'src/test/java'
}

jacoco {
    toolVersion = "0.7.5.201505241946"
    reportsDir = file("$buildDir/reports")
}

task jacocoTestReport(type: JacocoReport, dependsOn: "testDebugUnitTest") {
    group = "Reporting"
    description = "Generate Jacoco coverage reports for Debug build"

    reports {
        xml.enabled = true
        html.enabled = true
    }


    def excludes = ['**/R.class',
                    '**/R$*.class',
                    '**/BuildConfig.*',
                    '**/Manifest*.*',
                    'android/**/*.*',
                    '**/Lambda$*.class', //Retrolambda
                    '**/Lambda.class',
                    '**/*Lambda.class',
                    '**/*Lambda*.class',
                    '**/*Lambda*.*',
                    '**/*Builder.*',
                    '**/*_MembersInjector.class', //Dagger2 generated code
                    '**/*_MembersInjector*.*', //Dagger2 generated code
                    '**/*_Factory.*', //Dagger2 generated code
                    '**/*Component*.*', //Dagger2 generated code
                    '**/*Module*.*', //Dagger2 generated code
                    '**/*$ViewBinder*.*',
                    '**/*$InjectAdapter*.*',
                    '**/*Injector*.*',
                    '**/*Test*.*',
                    '**/*Activity*.*',
                    '**/CiMattersApplication*.*',
                    '**/**Creator**',
                    '**/**_Impl**',
                    '**/**.invokeSuspend**',
                    '**/**.Creator**',
                    '**/*_Provide*Factory*.*',
                    '**/*$*$*.*',
                    '**/**_ViewBinding**',
                    '**/**Companion**']

    def debugTree = fileTree(
            dir: "$buildDir/intermediates/javac/debug/compileDebugJavaWithJavac/classes/de/logerbyte/jacocotest",
            excludes: excludes)
    def kotlinDebugTree = fileTree(
            dir: "$buildDir/tmp/kotlin-classes/debug",
            excludes: excludes)
    def mainSrc = "${project.projectDir}/src/main/java"

    classDirectories = files([debugTree], [kotlinDebugTree])
    executionData = files("$buildDir/jacoco/testDebugUnitTest.exec")
    sourceDirectories = files([mainSrc])
}